plugins {
  id 'org.springframework.boot' version '2.6.10'
  id 'java'
  id 'jacoco'
  id 'checkstyle'
  id 'war'
  id 'org.sonarqube' version '2.8'
  id "org.owasp.dependencycheck" version "6.1.2"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'

group = 'uk.co.fivium'
version = System.getenv("DRONE_BRANCH") ? System.getenv("DRONE_BRANCH").replaceAll("[/\\-]", "_") : "LOCAL-SNAPSHOT"
sourceCompatibility = '11'

tasks {
  bootJar {
    archiveFileName.set("${rootProject.name}.jar")
  }
}

compileJava {
  options.annotationProcessorGeneratedSourcesDirectory = file("${buildDir}/generated")
}

repositories {
  mavenCentral()
  maven {
    url "https://nexus.fivium.co.uk/repository/maven-releases/"
  }
  maven {
    url "https://nexus.fivium.co.uk/repository/maven-snapshots/"
  }
}

dependencies {

  providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-jdbc'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-freemarker'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation ('org.springframework.boot:spring-boot-starter-quartz') {
    exclude group: 'com.zaxxer', module: 'HikariCP-java7'
    // workaround for https://github.com/spring-projects/spring-boot/issues/16489
  }
  implementation 'io.micrometer:micrometer-registry-statsd:1.7.8'

  implementation 'org.flywaydb:flyway-core:5.2.1-FIVIUM'
  implementation 'com.oracle:ojdbc7:12.1.0.2'
  implementation 'org.springframework.session:spring-session-jdbc'
  implementation 'uk.gov.service.notify:notifications-java-client:3.17.3-RELEASE'
  implementation('uk.co.fivium.feedbackmanagementservice:client:2.1.1')

  implementation 'com.google.guava:guava:31.0.1-jre'
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'org.apache.commons:commons-collections4:4.4'
  implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
  implementation group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: '8.12.8'
  implementation group: 'fi.solita.clamav', name: 'clamav-client', version: '1.0.1'

  annotationProcessor("org.hibernate:hibernate-jpamodelgen")
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

  testImplementation(
      'com.h2database:h2:1.4.197',
      'org.springframework.boot:spring-boot-starter-test',
      'org.springframework.security:spring-security-test',
      'com.squareup.okhttp3:mockwebserver:4.9.1',
      'com.squareup.okhttp3:okhttp:4.9.1', // Required otherwise MockWebServer gives ClassNotFoundException
      'org.junit.jupiter:junit-jupiter-api:5.9.2',
      'org.junit.vintage:junit-vintage-engine'
  )
}

test {
  useJUnitPlatform()
}

checkstyle {
  config = rootProject.resources.text.fromFile('ide/checkstyle.xml')
  toolVersion '8.28'
  ignoreFailures = false
  maxWarnings = 0
}

tasks.jacocoTestReport {
  reports {
    xml.enabled(true)
    html.enabled(false)
  }
}

dependencyCheck {
  data {
    // Point to a persisted volume in drone, to avoid having to re-download the whole CVE database each run.
    // Comment this out to run locally
    directory='/var/nvd-db'
  }
}

sonarqube {
  properties {
    property("sonar.projectKey", "fiviumuk_pathfinder")
    property("sonar.host.url", "https://sonarcloud.io")
    property("sonar.login", System.getenv("SONAR_TOKEN"))
    property("sonar.organization", "fivium")
    property("sonar.branch.name", System.getenv("DRONE_BRANCH"))
    if (System.getenv("DRONE_BRANCH").startsWith("feature")) {
      property("sonar.branch.target", "develop")
    }
    property("sonar.exclusions", "src/test/**, src/main/resources/public/assets/**, fivium-design-system-core/**, src/main/resources/templates/fds/**, build/**")
    property("sonar.sources", "src")
  }
}
